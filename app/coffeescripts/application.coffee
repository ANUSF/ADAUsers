$ = jQuery

#TODO do not rely on IDs generated by Rails

patterns =
  form:                'form#new_user'

  country_selection:   '#user_country_input select'
  affiliation_aus:     '#affiliation-aus'
  affiliation_non_aus: '#affiliation-non-aus'

  inst_type_container: '#user_austinstitution_input'
  inst_buttons_name:   'user[austinstitution]'

  inst_type_conditional_fields:
    Uni:   '#user_australian_uni_input'
    Dept:  '#user_australian_gov_input'
    Other: '#user_australian_other_inputs'


checked_radio_button = (scope, name) ->
  scope.find("input[@name='#{name}']:checked")


country_change = (item) ->
  form = item.closest patterns.form
  aus = form.find(patterns.affiliation_aus)
  other = form.find(patterns.affiliation_non_aus)

  if item.val() == "15" # code for Australia
    aus.show()
    other.hide()
  else
    aus.hide()
    other.show()


institution_change = (item) ->
  form = item.closest patterns.form
  value = checked_radio_button(form, patterns.inst_buttons_name).val()

  for k, v of patterns.inst_type_conditional_fields
    form.find(v)[if k == value then 'show' else 'hide']()


$(document).ready ->
  $(patterns.form).find(patterns.country_selection)
    .each(-> country_change $ this)
    .change(-> country_change $ this)
  $(patterns.form).find(patterns.inst_type_container)
    .each(-> institution_change $ this)
    .find('input').click(-> institution_change $ this)
