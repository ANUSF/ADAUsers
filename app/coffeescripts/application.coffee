$ = jQuery

#TODO do not rely on IDs generated by Rails

patterns =
  form:                'form.user'

  position_selection:  '#user_position_input select'
  other_position:      '#user_otherpd_input'

  action_selection:    '#user_action_input select'
  other_action:        '#user_otherwt_input'

  country_selection:   '#user_country_input select'
  affiliation_aus:     '#affiliation-aus'
  affiliation_non_aus: '#affiliation-non-aus'

  inst_type_container: '#user_austinstitution_input'
  inst_buttons_name:   'user[austinstitution]'

  inst_type_conditional_fields:
    Uni:   '#user_australian_uni_input'
    Dept:  '#user_australian_gov_input'
    Other: '#user_australian_other_inputs'


position_change = (item) ->
  other = item.closest(patterns.form).find(patterns.other_position)
  if item.val() == 'Other' then other.show() else other.hide()


action_change = (item) ->
  other = item.closest(patterns.form).find(patterns.other_action)
  if item.val() == 'Other' then other.show() else other.hide()


country_change = (item) ->
  form = item.closest patterns.form
  aus = form.find(patterns.affiliation_aus)
  other = form.find(patterns.affiliation_non_aus)

  if item.val() == "15" # code for Australia
    aus.show()
    other.hide()
  else
    aus.hide()
    other.show()


institution_change = (item) ->
  form = item.closest patterns.form
  pattern = "input[@name='#{patterns.inst_buttons_name}']:checked"
  value = form.find(pattern).val()

  for k, v of patterns.inst_type_conditional_fields
    form.find(v)[if k == value then 'show' else 'hide']()


process_registration_form = ->
  if (form = $ patterns.form)?
    form.find(patterns.position_selection)
      .each(-> position_change $ this)
      .change(-> position_change $ this)

    form.find(patterns.action_selection)
      .each(-> action_change $ this)
      .change(-> action_change $ this)

    form.find(patterns.country_selection)
      .each(-> country_change $ this)
      .change(-> country_change $ this)

    form.find(patterns.inst_type_container)
      .each(-> institution_change $ this)
      .find('input').click(-> institution_change $ this)


process_undertaking_form = ->
  if $("#undertaking_intended_use_type_input").length > 0
    # Show/hide supervisor's email address field based on whether thesis/coursework is an intended use
    handle_use_intended_use_type_thesis_click = ->
      checked = $("#undertaking_intended_use_type_thesis").is(":checked")
      field = $("li#undertaking_email_supervisor_input")
      if checked then field.slideDown() else field.slideUp()
    $("#undertaking_intended_use_type_thesis").click(handle_use_intended_use_type_thesis_click)
    handle_use_intended_use_type_thesis_click()


  if (form = $("form.undertaking"))? && $("#undertaking_catalogue").length > 0
    # For restricted form, handle catalogue dataset selection
    dataset_select = form.find("#undertaking_dataset_ids")
    dataset_select.empty()
    form.find("#undertaking_catalogue").change ->
      dataset_select.empty().addClass("loading")
      $.ajax
        url: "/datasets/restricted/"+$(this).val()
        success: (html) ->
          dataset_select.html(html)
        complete: ->
          dataset_select.removeClass("loading")

process_admin_undertaking_form = ->
  if $("table.admin-undertakings").length > 0
    $("tr.admin-undertaking-details div").hide()
    $("tr.admin-undertaking-summary").click ->
      $(this).next().find("div").slideToggle()

$(document).ready ->
  process_registration_form()
  process_undertaking_form()
  process_admin_undertaking_form()
  $("select.filterable").selectFilter()
  $(".radio-tabs").radioTabs()
